buildscript {
	ext {
		springBootVersion = '2.7.1'
	}
    repositories {  
        mavenLocal()    
        mavenCentral() 
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/snapshot" }
       
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}



/*
 * Extra Properties
 */
ext {
    /*
     * Software versions
     */
    mysqlConnectorVersion = "5.1.49"
  

}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'org.springframework.boot'
/*
 * The dependency management plugin remains a transitive dependency of spring-boot-gradle-plugin
 * so thereâ€™s no need for it to be listed as a classpath dependency in the buildscript configuration.
 */
apply plugin: 'io.spring.dependency-management'
/*
apply plugin: 'maven'
*/
apply plugin: 'maven-publish'



group = "gov.nih.nci.evs.reportwriter.web"
version = "1.0.9.RELEASE"
sourceCompatibility = 1.8


springBoot {
    buildInfo()          // generate file META-INF/build-info.properties during the build
}

repositories {
    mavenLocal()  
    mavenCentral() 
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/snapshot" }
}

dependencies {
    implementation ('org.springframework.boot:spring-boot-starter-web')
    
    implementation ('org.springframework.boot:spring-boot-starter-data-rest')
    implementation ('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation 'org.springframework.boot:spring-boot-actuator'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-properties-migrator'
    
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'




    implementation "mysql:mysql-connector-java:$mysqlConnectorVersion"
    implementation "gov.nih.nci.evs.reportwriter.core:reportwriter-core:1.0.9-SNAPSHOT"

    /*
     * Test Dependencies
     */
   
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}



jar {
    from('frontend/dist') {
        //Public is a default supported Spring Boot resources directory.
        into 'public'
    }
}
 
//frontend:build will be run before the processResources
processResources.dependsOn('frontend:build')


test {
    /*
     * Configure gradle to show standard out and standard error
     * of the test JVM(s) on the console
     */
    testLogging.showStandardStreams = true

    
}

task zipFile(type: Zip) {
    def zipFileRootDirectory = "${project.name}-${project.version}"

    from('src/main/bin') {
        fileMode = 0755
        into "$zipFileRootDirectory/bin"
    }
    from("$buildDir/libs") {
        include '*.war'
        into "$zipFileRootDirectory/lib"
    }
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}


zipFile.dependsOn = [ bootWar ]
build.dependsOn = [ test, bootWar, zipFile ]
publish.dependsOn = [ bootWar, zipFile ]

